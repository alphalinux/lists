<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<HTML>
<HEAD>
<TITLE>axp-list: patch_v3: emulating missing instructions on Alpha</TITLE>
<META NAME="Author" CONTENT="Luke Deller (luked@cse.unsw.edu.au)">
<META NAME="Subject" CONTENT="patch_v3: emulating missing instructions on Alpha">
</HEAD>
<BODY BGCOLOR="#FFFFFF" TEXT="#000000">
<H1 ALIGN=CENTER>patch_v3: emulating missing instructions on Alpha</H1>
<HR>
<P>
<!-- received="Thu Oct 21 19:22:41 1999" -->
<!-- isoreceived="19991022022241" -->
<!-- sent="Fri, 22 Oct 1999 02:23:22 +1000 (EST)" -->
<!-- isosent="19991021162322" -->
<!-- name="Luke Deller" -->
<!-- email="luked@cse.unsw.edu.au" -->
<!-- subject="patch_v3: emulating missing instructions on Alpha" -->
<!-- id="Pine.OSF.3.95.991022015853.2383C-200000@ganter.disy.cse.unsw.EDU.AU" -->
<!-- inreplyto="199910181446.QAA18379@pauillac.inria.fr" -->
<STRONG>Subject: </STRONG>patch_v3: emulating missing instructions on Alpha<BR>
<STRONG>From: </STRONG>Luke Deller (<EM>luked@cse.unsw.edu.au</EM>)<BR>
<STRONG>Date: </STRONG>Thu Oct 21 1999 - 09:23:22 PDT
<P>
<UL>
<LI><STRONG>Messages sorted by:</STRONG> 
<A HREF="date.html#500">[ date ]</A>
<A HREF="index.html#500">[ thread ]</A>
<A HREF="subject.html#500">[ subject ]</A>
<A HREF="author.html#500">[ author ]</A>
<!-- next="start" -->
<LI><STRONG>Next message:</STRONG> <A HREF="0501.html">Brad Lucier: "Re: Serious problems on DS20"</A>
<LI><STRONG>Previous message:</STRONG> <A HREF="0499.html">Champigny, Michael: "Ethernet on Multia solved..."</A>
<LI><STRONG>In reply to:</STRONG> <A HREF="0405.html">Robert Harley: "Re: patch_v2: (on Alpha) emulating missing instructions"</A>
<!-- nextthread="start" -->
<LI><STRONG>Next in thread:</STRONG> <A HREF="0592.html">Richard Henderson: "Re: patch_v3: emulating missing instructions on Alpha"</A>
<LI><STRONG>Reply:</STRONG> <A HREF="0405.html">Luke Deller: "patch_v3: emulating missing instructions on Alpha"</A>
<!-- reply="end" -->
</UL>
<HR>
<!-- body="start" -->
<P>
Thanks Robert for your optimisations and bugspotting. I've incorporated them
<BR>
into our emulation code.  Also I have completed the emulation of the
<BR>
multimedia instructions (not just what we needed to run Civilisation CTP) by
<BR>
adding emulation for:
<BR>
- &quot;pixel error&quot;(perr)
<BR>
- word/long pack to byte (pkwb,pklb) and unpack (unpkbw,unpkbl)
<BR>
<P>I've attached the new patch to this email so that some of you guys can check
<BR>
it out.  Is this sort of code useful to other people?  Do you think it's
<BR>
worth adding this to the mainstream Linux distribution?
<BR>
<P>Again, this is a patch for kernel v2.2.11 because Loki say that v2.2.12 has
<BR>
problems which break Civilisation CTP.  Our purpose in writing this patch
<BR>
was to get CivCTP to run.  It doesn't change many files though, so it will
<BR>
probably work with other kernel versions.
<BR>
<P>- Luke &amp; Daniel.
<BR>
<P>On Mon, 18 Oct 1999, Robert Harley wrote:
<BR>
<P><EM>&gt; BTW, here is a fast and short way of doing minuw():
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; ------------------------------------------------------------------------------
</EM><BR>
<EM>&gt; #define CMPBGE(a, b, c) \
</EM><BR>
<EM>&gt;   asm(&quot;cmpbge %1, %2, %0&quot; : &quot;=r&quot; (c) : &quot;r&quot; (a), &quot;r&quot; (b))
</EM><BR>
<EM>&gt; #define ZAP(a, b, c) \
</EM><BR>
<EM>&gt;   asm(&quot;zap %1, %2, %0&quot; : &quot;=r&quot; (c) : &quot;r&quot; (a), &quot;r&quot; (b))
</EM><BR>
<EM>&gt; #define ZAPNOT(a, b, c) \
</EM><BR>
<EM>&gt;   asm(&quot;zapnot %1, %2, %0&quot; : &quot;=r&quot; (c) : &quot;r&quot; (a), &quot;r&quot; (b))
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; static u64 minuw(u64 x, u64 y) {
</EM><BR>
<EM>&gt;   u64 eq, ge1, ge2;
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;   CMPBGE(x, y, ge1);
</EM><BR>
<EM>&gt;   CMPBGE(0, x ^ y, eq);
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;   ge2 = (ge1 &amp; ~eq | eq &amp; ge1&lt;&lt;1) &amp; 0xAA;
</EM><BR>
<EM>&gt;   ge2 |= ge2&gt;&gt;1;
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;   ZAP(x, ge2, x);
</EM><BR>
<EM>&gt;   ZAPNOT(y, ge2, y);
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;   return x | y;
</EM><BR>
<EM>&gt; } /* end function minuw */
</EM><BR>
<EM>&gt; ------------------------------------------------------------------------------
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; The output from gcc 2.95 looks like:
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; ------------------------------------------------------------------------------
</EM><BR>
<EM>&gt; minuw:
</EM><BR>
<EM>&gt;         cmpbge $16, $17, $1
</EM><BR>
<EM>&gt;         xor $16,$17,$3
</EM><BR>
<EM>&gt;         mov $31,$2
</EM><BR>
<EM>&gt;         cmpbge $2, $3, $2
</EM><BR>
<EM>&gt;         addq $1,$1,$4
</EM><BR>
<EM>&gt;         bic $1,$2,$1
</EM><BR>
<EM>&gt;         and $2,$4,$2
</EM><BR>
<EM>&gt;         bis $1,$2,$1
</EM><BR>
<EM>&gt;         and $1,170,$1
</EM><BR>
<EM>&gt;         srl $1,1,$2
</EM><BR>
<EM>&gt;         bis $1,$2,$1
</EM><BR>
<EM>&gt;         zapnot $17, $1, $17
</EM><BR>
<EM>&gt;         zap $16, $1, $16
</EM><BR>
<EM>&gt;         bis $16,$17,$0
</EM><BR>
<EM>&gt;         ret $31,($26),1
</EM><BR>
<EM>&gt;         .end minuw
</EM><BR>
<EM>&gt; ------------------------------------------------------------------------------
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; This bit:
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;         mov $31,$2
</EM><BR>
<EM>&gt;         cmpbge $2, $3, $2
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; would be better as:
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt;         cmpbge $31, $3, $2
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; 
</EM><BR>
<EM>&gt; Bye,
</EM><BR>
<EM>&gt;   Rob.
</EM><BR>
<P>--------------------------------------------------------------------------
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Luke Deller                               luked@cse.unsw.edu.au
<BR>
&nbsp;_--_|\    Computer Engineering student           
<BR>
/      \   School of Computer Science and Engineering     ? ? ? ?        
<BR>
\_.--._*   The University of New South Wales               ~~^^~  
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;v    Sydney, NSW     2052    AUSTRALIA                o o
<BR>
-------------------------------------------------------ooO--(_)--Ooo------
<BR>
<P><P><P>
diff -u --recursive --new-file v2.2.11/linux/Documentation/Configure.help linux/Documentation/Configure.help
<BR>
--- v2.2.11/linux/Documentation/Configure.help	Tue Aug 10 05:04:57 1999
<BR>
+++ linux/Documentation/Configure.help	Thu Oct 21 17:58:27 1999
<BR>
@@ -11588,8 +11588,22 @@
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;Everyone using these boards should say Y here.  
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;See &quot;linux/Documentation/cpqarray.txt&quot; for the current list of 
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;boards supported by this driver, and for further information 
<BR>
-   on the use of this driver. 
<BR>
- 
<BR>
+   on the use of this driver.
<BR>
+
<BR>
+Emulation of some extended alpha instructions
<BR>
+CONFIG_ALPHA_EXTINSNS
<BR>
+  This emulates some instructions which are not implemented in hardware
<BR>
+  on some alpha CPU's.  Emulation is provided for byte/word load/store
<BR>
+  instructions (in the BWX instruction set extension) and the multimedia
<BR>
+  extension instructions.  If you say Y here then this emulation code will
<BR>
+  be included in your kernel.  Any of these instructions which your
<BR>
+  hardware supports will not be emulated even if you have included the
<BR>
+  emulation code in your kernel.  If unsure, say N.
<BR>
+
<BR>
+  Here is the list of emulated instructions:
<BR>
+  maxuw, maxsw, minuw, minsw, maxub, maxsb, minsb, minub,
<BR>
+  sexb, sexw, perr, pklb, pkwb, unpkbl, unpkbw, stb, stw, ldbu, ldwu.
<BR>
+
<BR>
&nbsp;#
<BR>
&nbsp;# A couple of things I keep forgetting:
<BR>
&nbsp;#   capitalize: AppleTalk, Ethernet, DOS, DMA, FAT, FTP, Internet, 
<BR>
diff -u --recursive --new-file v2.2.11/linux/arch/alpha/config.in linux/arch/alpha/config.in
<BR>
--- v2.2.11/linux/arch/alpha/config.in	Sun May 23 06:41:37 1999
<BR>
+++ linux/arch/alpha/config.in	Fri Oct 22 02:18:51 1999
<BR>
@@ -280,6 +280,7 @@
<BR>
&nbsp;#bool 'Debug kmalloc/kfree' CONFIG_DEBUG_MALLOC
<BR>
&nbsp;if [ &quot;$CONFIG_EXPERIMENTAL&quot; = &quot;y&quot; ]; then
<BR>
&nbsp;&nbsp;&nbsp;tristate 'Kernel FP software completion' CONFIG_MATHEMU
<BR>
+  bool 'Emulation of some extended instructions' CONFIG_ALPHA_EXTINSNS
<BR>
&nbsp;else
<BR>
&nbsp;&nbsp;&nbsp;define_bool CONFIG_MATHEMU y
<BR>
&nbsp;fi
<BR>
diff -u --recursive --new-file v2.2.11/linux/arch/alpha/defconfig linux/arch/alpha/defconfig
<BR>
--- v2.2.11/linux/arch/alpha/defconfig	Sun Jun 13 04:52:51 1999
<BR>
+++ linux/arch/alpha/defconfig	Tue Oct 19 01:15:28 1999
<BR>
@@ -330,4 +330,5 @@
<BR>
&nbsp;# Kernel hacking
<BR>
&nbsp;#
<BR>
&nbsp;CONFIG_MATHEMU=y
<BR>
+# CONFIG_ALPHA_EXTINSNS is not set
<BR>
&nbsp;# CONFIG_MAGIC_SYSRQ is not set
<BR>
diff -u --recursive --new-file v2.2.11/linux/arch/alpha/kernel/entry.S linux/arch/alpha/kernel/entry.S
<BR>
--- v2.2.11/linux/arch/alpha/kernel/entry.S	Tue Aug 10 05:04:57 1999
<BR>
+++ linux/arch/alpha/kernel/entry.S	Fri Oct 22 01:43:03 1999
<BR>
@@ -39,6 +39,13 @@
<BR>
&nbsp;#define	PF_PTRACED	0x00000010
<BR>
&nbsp;
<BR>
&nbsp;/*
<BR>
+ * This is used to tell if a memory address is in user land.
<BR>
+ * This must be consistent with USER_DS in include/asm-alpha/uaccess.h 
<BR>
+ * and TASK_SIZE in include/asm-alpha/processor.h
<BR>
+ */
<BR>
+#define LOG2_TASKSIZE 42
<BR>
+
<BR>
+/*
<BR>
&nbsp;&nbsp;* This defines the normal kernel pt-regs layout.
<BR>
&nbsp;&nbsp;*
<BR>
&nbsp;&nbsp;* regs 9-15 preserved by C code
<BR>
@@ -173,10 +180,1038 @@
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;jsr	$31,do_entArith
<BR>
&nbsp;.end entArith
<BR>
&nbsp;
<BR>
+#ifdef CONFIG_ALPHA_EXTINSNS
<BR>
+/* emulation of some extended alpha instructions */
<BR>
+
<BR>
+/* This emulation code doesn't handle segfaults as nicely as it should ..
<BR>
+ * it just faults in kernel mode (resulting in the user task being killed)
<BR>
+ * rather than pretending that the fault happened in userland (which should
<BR>
+ * result in a SIGSEGV being sent to the user task).
<BR>
+ *
<BR>
+ * The emulation code ensures that the user doesn't access kernel space by
<BR>
+ * checking that the high bits of the load/store virtual address are zero.
<BR>
+ */
<BR>
+
<BR>
+.align 3
<BR>
+.ent	get_user_sp
<BR>
+/* gets the value of the user's stack pointer into $3 */
<BR>
+get_user_sp:
<BR>
+	subq		$30,6*8,$30
<BR>
+	stq		$0,0($30)
<BR>
+	stq		$1,8($30)
<BR>
+	stq		$16,16($30)
<BR>
+	mov		$25,$3
<BR>
+	stq		$22,24($30)
<BR>
+	stq		$23,32($30)
<BR>
+	stq		$24,40($30)
<BR>
+	call_pal	PAL_rdusp
<BR>
+	ldq		$24,40($30)
<BR>
+	mov		$3,$25
<BR>
+	ldq		$23,32($30)
<BR>
+	ldq		$22,24($30)
<BR>
+	mov		$0,$3
<BR>
+	ldq		$16,16($30)
<BR>
+	ldq		$1,8($30)
<BR>
+	ldq		$0,0($30)
<BR>
+	addq		$30,6*8,$30
<BR>
+	ret		$31,($5)
<BR>
+.end	get_user_sp
<BR>
+
<BR>
+.align 3
<BR>
+.ent	put_user_sp
<BR>
+/* puts the value from $3 into the user's stack pointer */
<BR>
+put_user_sp:
<BR>
+	subq		$30,5*8,$30
<BR>
+	stq		$16,0($30)
<BR>
+	stq		$22,8($30)
<BR>
+	stq		$23,16($30)
<BR>
+	mov		$3,$16
<BR>
+	stq		$24,24($30)
<BR>
+	mov		$1,$3
<BR>
+	stq		$25,32($30)
<BR>
+	call_pal	PAL_wrusp
<BR>
+	ldq		$25,32($30)
<BR>
+	ldq		$24,24($30)
<BR>
+	mov		$3,$1
<BR>
+	ldq		$23,16($30)
<BR>
+	ldq		$22,8($30)
<BR>
+	ldq		$16,0($30)
<BR>
+	addq		$30,5*8,$30
<BR>
+	ret		$31,($5)
<BR>
+.end	put_user_sp
<BR>
+
<BR>
+/* this macro trashes $5 &amp; index, and returns user reg val in $3. */
<BR>
+/* index should be a register containing a value from 0 to 31. */
<BR>
+#define get_user_reg(index)           \
<BR>
+	br	$3,1f                 ;\
<BR>
+1:	                              ;\
<BR>
+	s8addq	index,$3,index        ;\
<BR>
+	lda	$5,get_ureg0-1b($31)  ;\
<BR>
+	addq	index,$5,index        ;\
<BR>
+	jsr	$5,(index),get_ureg16
<BR>
+
<BR>
+/* this macro takes new user reg val in $3, and trashes $3,$5 &amp; index. */
<BR>
+/* index should be a register containing a value from 0 to 31. */
<BR>
+#define put_user_reg(index)           \
<BR>
+	br	$5,1f                 ;\
<BR>
+1:	                              ;\
<BR>
+	s8addq	index,$5,index        ;\
<BR>
+	lda	$5,put_ureg0-1b($31)  ;\
<BR>
+	addq	index,$5,index        ;\
<BR>
+	jsr	$5,(index),put_ureg16
<BR>
+
<BR>
+/* a code table .. make sure each entry is exactly 2 instructions. */
<BR>
+/* used by get_user_reg macro. */
<BR>
+.align 3
<BR>
+.ent	get_ureg_table
<BR>
+get_ureg0:
<BR>
+	ldq	$3,8*0($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg1:
<BR>
+	ldq	$3,8*1($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg2:
<BR>
+	ldq	$3,8*2($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg3:
<BR>
+	ldq	$3,8*3($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg4:
<BR>
+	ldq	$3,8*4($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg5:
<BR>
+	ldq	$3,8*5($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg6:
<BR>
+	mov	$6,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg7:
<BR>
+	mov	$7,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg8:
<BR>
+	mov	$8,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg9:
<BR>
+	mov	$9,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg10:
<BR>
+	mov	$10,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg11:
<BR>
+	mov	$11,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg12:
<BR>
+	mov	$12,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg13:
<BR>
+	mov	$13,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg14:
<BR>
+	mov	$14,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg15:
<BR>
+	mov	$15,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg16:
<BR>
+	ldq	$3,8*10($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg17:
<BR>
+	ldq	$3,8*11($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg18:
<BR>
+	ldq	$3,8*12($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg19:
<BR>
+	mov	$19,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg20:
<BR>
+	mov	$20,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg21:
<BR>
+	mov	$21,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg22:
<BR>
+	mov	$22,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg23:
<BR>
+	mov	$23,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg24:
<BR>
+	mov	$24,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg25:
<BR>
+	mov	$25,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg26:
<BR>
+	mov	$26,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg27:
<BR>
+	mov	$27,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg28:
<BR>
+	mov	$28,$3
<BR>
+	ret	$31,($5)
<BR>
+get_ureg29:
<BR>
+	ldq	$3,9*8($30)
<BR>
+	ret	$31,($5)
<BR>
+get_ureg30:
<BR>
+	br	$31,get_user_sp 
<BR>
+	ret	$31,($5)
<BR>
+get_ureg31:
<BR>
+	mov	$31,$3
<BR>
+	ret	$31,($5)
<BR>
+.end	get_ureg_table
<BR>
+
<BR>
+/* another code table .. make sure each entry is exactly 2 instructions. */
<BR>
+/* used by put_user_reg macro. */
<BR>
+.align 3
<BR>
+.ent	put_ureg_table
<BR>
+put_ureg0:
<BR>
+	stq	$3,8*0($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg1:
<BR>
+	stq	$3,8*1($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg2:
<BR>
+	stq	$3,8*2($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg3:
<BR>
+	stq	$3,8*3($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg4:
<BR>
+	stq	$3,8*4($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg5:
<BR>
+	stq	$3,8*5($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg6:
<BR>
+	mov	$3,$6
<BR>
+	ret	$31,($5)
<BR>
+put_ureg7:
<BR>
+	mov	$3,$7
<BR>
+	ret	$31,($5)
<BR>
+put_ureg8:
<BR>
+	mov	$3,$8
<BR>
+	ret	$31,($5)
<BR>
+put_ureg9:
<BR>
+	mov	$3,$9
<BR>
+	ret	$31,($5)
<BR>
+put_ureg10:
<BR>
+	mov	$3,$10
<BR>
+	ret	$31,($5)
<BR>
+put_ureg11:
<BR>
+	mov	$3,$11
<BR>
+	ret	$31,($5)
<BR>
+put_ureg12:
<BR>
+	mov	$3,$12
<BR>
+	ret	$31,($5)
<BR>
+put_ureg13:
<BR>
+	mov	$3,$13
<BR>
+	ret	$31,($5)
<BR>
+put_ureg14:
<BR>
+	mov	$3,$14
<BR>
+	ret	$31,($5)
<BR>
+put_ureg15:
<BR>
+	mov	$3,$15
<BR>
+	ret	$31,($5)
<BR>
+put_ureg16:
<BR>
+	stq	$3,8*10($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg17:
<BR>
+	stq	$3,8*11($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg18:
<BR>
+	stq	$3,8*12($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg19:
<BR>
+	mov	$3,$19
<BR>
+	ret	$31,($5)
<BR>
+put_ureg20:
<BR>
+	mov	$3,$20
<BR>
+	ret	$31,($5)
<BR>
+put_ureg21:
<BR>
+	mov	$3,$21
<BR>
+	ret	$31,($5)
<BR>
+put_ureg22:
<BR>
+	mov	$3,$22
<BR>
+	ret	$31,($5)
<BR>
+put_ureg23:
<BR>
+	mov	$3,$23
<BR>
+	ret	$31,($5)
<BR>
+put_ureg24:
<BR>
+	mov	$3,$24
<BR>
+	ret	$31,($5)
<BR>
+put_ureg25:
<BR>
+	mov	$3,$25
<BR>
+	ret	$31,($5)
<BR>
+put_ureg26:
<BR>
+	mov	$3,$26
<BR>
+	ret	$31,($5)
<BR>
+put_ureg27:
<BR>
+	mov	$3,$27
<BR>
+	ret	$31,($5)
<BR>
+put_ureg28:
<BR>
+	mov	$3,$28
<BR>
+	ret	$31,($5)
<BR>
+put_ureg29:
<BR>
+	stq	$3,9*8($30)
<BR>
+	ret	$31,($5)
<BR>
+put_ureg30:
<BR>
+	br	$31,put_user_sp 
<BR>
+	ret	$31,($5)
<BR>
+put_ureg31:
<BR>
+	ret	$31,($5)
<BR>
+.end	put_ureg_table
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_ldwu
<BR>
+.ent	emul_ldwu
<BR>
+/* emulating a &quot;load word unsigned&quot; instruction of the form:
<BR>
+ * 	ldwu	$ra,displacement($rb)
<BR>
+ */
<BR>
+emul_ldwu:
<BR>
+	stq	$3,24($30)
<BR>
+	stq	$4,32($30)
<BR>
+	stq	$5,40($30)
<BR>
+
<BR>
+	srl	$0,21,$1
<BR>
+	srl	$0,16,$2
<BR>
+	and	$1,0x1f,$1
<BR>
+	sll	$0,48,$0
<BR>
+	and	$2,0x1f,$2
<BR>
+	sra	$0,48,$0   /* $0 = displacement */
<BR>
+	get_user_reg($2)   /* $3 = $rb  */
<BR>
+	addq	$0,$3,$0
<BR>
+	srl	$0,LOG2_TASKSIZE,$5 /* if addr is not in userland, */
<BR>
+	cmovne	$5,$31,$0       /* then force a segfault by putting addr=0 */
<BR>
+	ldq_u	$3,0($0)
<BR>
+	ldq_u	$5,1($0)
<BR>
+	extwl	$3,$0,$3
<BR>
+	extwh	$5,$0,$5
<BR>
+	or	$3,$5,$3
<BR>
+	put_user_reg($1)   /* $ra = loaded val */
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_ldwu
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_stw
<BR>
+.ent	emul_stw
<BR>
+emul_stw:
<BR>
+/* emulating a &quot;store word&quot; instruction of the form:
<BR>
+ * 	stw	$ra,displacement($rb)
<BR>
+ */
<BR>
+	stq	$3,24($30)
<BR>
+	stq	$4,32($30)
<BR>
+	stq	$5,40($30)
<BR>
+
<BR>
+	srl	$0,21,$1
<BR>
+	srl	$0,16,$2
<BR>
+	and	$1,0x1f,$1
<BR>
+	sll	$0,48,$0
<BR>
+	and	$2,0x1f,$2
<BR>
+	sra	$0,48,$0   /* $0 = displacement */
<BR>
+	get_user_reg($1)
<BR>
+	mov	$3,$1      /* $1 = $ra val */
<BR>
+	get_user_reg($2)   /* $3 = $rb val */
<BR>
+	addq	$0,$3,$0
<BR>
+	srl	$0,LOG2_TASKSIZE,$5 /* if addr is not in userland, */
<BR>
+	cmovne	$5,$31,$0       /* then force a segfault by putting addr=0 */
<BR>
+	ldq_u	$3,1($0)
<BR>
+	ldq_u	$2,0($0)
<BR>
+	inswh	$1,$0,$5
<BR>
+	inswl	$1,$0,$4
<BR>
+	mskwh	$3,$0,$3
<BR>
+	mskwl	$2,$0,$2
<BR>
+	or	$3,$5,$3
<BR>
+	or	$2,$4,$2
<BR>
+	stq_u	$3,1($0)
<BR>
+	stq_u	$2,0($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_stw
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_ldbu
<BR>
+.ent	emul_ldbu
<BR>
+/* emulating a &quot;load byte unsigned&quot; instruction of the form:
<BR>
+ * 	ldbu	$ra,displacement($rb)
<BR>
+ */
<BR>
+emul_ldbu:
<BR>
+	stq	$3,24($30)
<BR>
+	stq	$4,32($30)
<BR>
+	stq	$5,40($30)
<BR>
+
<BR>
+	srl	$0,21,$1
<BR>
+	srl	$0,16,$2
<BR>
+	and	$1,0x1f,$1 
<BR>
+	sll	$0,48,$0
<BR>
+	and	$2,0x1f,$2
<BR>
+	sra	$0,48,$0   /* $0 = displacement */
<BR>
+	get_user_reg($2)   /* $3 = $ra val */
<BR>
+	addq	$0,$3,$0
<BR>
+	srl	$0,LOG2_TASKSIZE,$5 /* if addr is not in userland, */
<BR>
+	cmovne	$5,$31,$0       /* then force a segfault by putting addr=0 */
<BR>
+	ldq_u	$3,0($0)
<BR>
+	extbl	$3,$0,$3
<BR>
+	put_user_reg($1)   /* $rb := loaded val */
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_ldbu
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_stb
<BR>
+.ent	emul_stb
<BR>
+emul_stb:
<BR>
+/* emulating a &quot;store byte&quot; instruction of the form:
<BR>
+ * 	stb	$ra,displacement($rb)
<BR>
+ */
<BR>
+	stq	$3,24($30)
<BR>
+	stq	$4,32($30)
<BR>
+	stq	$5,40($30)
<BR>
+
<BR>
+	srl	$0,21,$1
<BR>
+	srl	$0,16,$2
<BR>
+	and	$1,0x1f,$1
<BR>
+	sll	$0,48,$0
<BR>
+	and	$2,0x1f,$2
<BR>
+	sra	$0,48,$0   /* $0 = displacement */
<BR>
+	get_user_reg($1)
<BR>
+	mov	$3,$1      /* $1 = $ra val */
<BR>
+	get_user_reg($2)   /* $3 = $rb val */
<BR>
+	addq	$0,$3,$0
<BR>
+	srl	$0,LOG2_TASKSIZE,$5 /* if addr is not in userland, */
<BR>
+	cmovne	$5,$31,$0       /* then force a segfault by putting addr=0 */
<BR>
+	ldq_u	$2,0($0)
<BR>
+	insbl	$1,$0,$3
<BR>
+	mskbl	$2,$0,$2
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	or	$2,$3,$2
<BR>
+
<BR>
+	ldq	$3,24($30)
<BR>
+	stq_u	$2,0($0)
<BR>
+
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_stb
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_minsw_4
<BR>
+.ent	emul_minsw_4
<BR>
+/* emulating a &quot;vector signed word minimum&quot; instruction of the form:
<BR>
+ * 	minsw4	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	minsw4	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_minsw_4:
<BR>
+	stq	$6,48($30)
<BR>
+
<BR>
+	not	$31,$1
<BR>
+	zap	$1,0x33,$1
<BR>
+	srl	$1,1,$5
<BR>
+	xor	$1,$5,$1  /* $1 = 0x8000800080008000 */
<BR>
+	xor	$1,$3,$6
<BR>
+	xor	$1,$2,$5
<BR>
+
<BR>
+	cmpbge	$6,$5,$1
<BR>
+	xor	$6,$5,$5
<BR>
+	cmpbge	$31,$5,$4
<BR>
+	addq	$1,$1,$6
<BR>
+	bic	$1,$4,$1
<BR>
+	and	$4,$6,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	and	$1,0xaa,$1
<BR>
+	srl	$1,1,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	zapnot	$2,$1,$2
<BR>
+	zap	$3,$1,$3
<BR>
+	bis	$3,$2,$3
<BR>
+
<BR>
+	ldq	$6,48($30)
<BR>
+
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_minsw_4
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_minuw_4
<BR>
+.ent	emul_minuw_4
<BR>
+/* emulating a &quot;vector unsigned word maximum&quot; instruction of the form:
<BR>
+ * 	minuw4	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	minuw4	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_minuw_4:
<BR>
+	stq	$6,48($30)
<BR>
+
<BR>
+	cmpbge	$3,$2,$1
<BR>
+	xor	$3,$2,$5
<BR>
+	cmpbge	$31,$5,$4
<BR>
+	addq	$1,$1,$6
<BR>
+	bic	$1,$4,$1
<BR>
+	and	$4,$6,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	and	$1,0xaa,$1
<BR>
+	srl	$1,1,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	zapnot	$2,$1,$2
<BR>
+	zap	$3,$1,$3
<BR>
+	bis	$3,$2,$3
<BR>
+
<BR>
+	ldq	$6,48($30)
<BR>
+
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_minuw_4
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_maxsw_4
<BR>
+.ent	emul_maxsw_4
<BR>
+/* emulating a &quot;vector signed word maximum&quot; instruction of the form:
<BR>
+ * 	maxsw4	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	maxsw4	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_maxsw_4:
<BR>
+	stq	$6,48($30)
<BR>
+
<BR>
+	not	$31,$1
<BR>
+	zap	$1,0x33,$1
<BR>
+	srl	$1,1,$5
<BR>
+	xor	$1,$5,$1  /* $1 = 0x8000800080008000 */
<BR>
+	xor	$1,$3,$6
<BR>
+	xor	$1,$2,$5
<BR>
+
<BR>
+	cmpbge	$6,$5,$1
<BR>
+	xor	$6,$5,$5
<BR>
+	cmpbge	$31,$5,$4
<BR>
+	addq	$1,$1,$6
<BR>
+	bic	$1,$4,$1
<BR>
+	and	$4,$6,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	and	$1,0xaa,$1
<BR>
+	srl	$1,1,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	zap	$2,$1,$2
<BR>
+	zapnot	$3,$1,$3
<BR>
+	bis	$3,$2,$3
<BR>
+
<BR>
+	ldq	$6,48($30)
<BR>
+
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_maxsw_4
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_maxuw_4
<BR>
+.ent	emul_maxuw_4
<BR>
+/* emulating a &quot;vector unsigned word maximum&quot; instruction of the form:
<BR>
+ * 	maxuw4	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	maxuw4	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_maxuw_4:
<BR>
+	stq	$6,48($30)
<BR>
+
<BR>
+	cmpbge	$3,$2,$1
<BR>
+	xor	$3,$2,$5
<BR>
+	cmpbge	$31,$5,$4
<BR>
+	addq	$1,$1,$6
<BR>
+	bic	$1,$4,$1
<BR>
+	and	$4,$6,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	and	$1,0xaa,$1
<BR>
+	srl	$1,1,$4
<BR>
+	bis	$1,$4,$1
<BR>
+	zap	$2,$1,$2
<BR>
+	zapnot	$3,$1,$3
<BR>
+	bis	$3,$2,$3
<BR>
+
<BR>
+	ldq	$6,48($30)
<BR>
+
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_maxuw_4
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_maxub_8
<BR>
+.ent	emul_maxub_8
<BR>
+/* emulating a &quot;vector unsigned byte maximum&quot; instruction of the form:
<BR>
+ * 	maxub8	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	maxub8	$ra,immediate8,$rc
<BR>
+ */
<BR>
+emul_maxub_8:
<BR>
+	cmpbge	$2,$3,$1
<BR>
+	zap	$3,$1,$3
<BR>
+	zapnot	$2,$1,$2
<BR>
+	or	$3,$2,$3
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_maxub_8
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_maxsb_8
<BR>
+.ent	emul_maxsb_8
<BR>
+/* emulating a &quot;vector signed byte maximum&quot; instruction of the form:
<BR>
+ * 	maxsb8	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	maxsb8	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_maxsb_8:
<BR>
+	not	$31,$1
<BR>
+	zap	$1,0x55,$1
<BR>
+	srl	$1,1,$5
<BR>
+	xor	$1,$5,$1  /* $1 = 0x8080808080808080 */
<BR>
+	xor	$3,$1,$5
<BR>
+	xor	$2,$1,$1
<BR>
+
<BR>
+	cmpbge	$1,$5,$1
<BR>
+	zap	$3,$1,$3
<BR>
+	zapnot	$2,$1,$2
<BR>
+	or	$3,$2,$3
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_maxsb_8
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_minub_8
<BR>
+.ent	emul_minub_8
<BR>
+/* emulating a &quot;vector unsigned byte minimum&quot; instruction of the form:
<BR>
+ * 	minsb8	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	minsb8	$ra,immediate8,$rc
<BR>
+ */
<BR>
+emul_minub_8:
<BR>
+	cmpbge	$3,$2,$1
<BR>
+	zap	$3,$1,$3
<BR>
+	zapnot	$2,$1,$2
<BR>
+	or	$3,$2,$3
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_minub_8
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_minsb_8
<BR>
+.ent	emul_minsb_8
<BR>
+/* emulating a &quot;vector unsigned byte maximum&quot; instruction of the form:
<BR>
+ * 	maxsb8	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	maxsb8	$ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_minsb_8:
<BR>
+	not	$31,$1
<BR>
+	zap	$1,0x55,$1
<BR>
+	srl	$1,1,$5
<BR>
+	xor	$1,$5,$1  /* $1 = 0x8080808080808080 */
<BR>
+	xor	$3,$1,$5
<BR>
+	xor	$2,$1,$1
<BR>
+
<BR>
+	cmpbge	$5,$1,$1
<BR>
+	zap	$3,$1,$3
<BR>
+	zapnot	$2,$1,$2
<BR>
+	or	$3,$2,$3
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_minsb_8
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_sexw
<BR>
+.ent	emul_sexw
<BR>
+/* emulating a &quot;word sign extend&quot; instruction of the form:
<BR>
+ * 	sexw	$rb,$rc
<BR>
+ * or
<BR>
+ *	sexw	immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_sexw:
<BR>
+	sll	$3,48,$3
<BR>
+	and	$0,0x1f,$1
<BR>
+	sra	$3,48,$3
<BR>
+	put_user_reg($1)
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_sexw
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_sexb
<BR>
+.ent	emul_sexb
<BR>
+/* emulating a &quot;byte sign extend&quot; instruction of the form:
<BR>
+ * 	sexb	$rb,$rc
<BR>
+ * or
<BR>
+ *	sexb	immediate8,$rc
<BR>
+ *
<BR>
+ * (called by emul_mmx below)
<BR>
+ */
<BR>
+emul_sexb:
<BR>
+	sll	$3,56,$3
<BR>
+	and	$0,0x1f,$1
<BR>
+	sra	$3,56,$3
<BR>
+	put_user_reg($1)
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_sexb
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_perr
<BR>
+.ent	emul_perr
<BR>
+/* emulating a &quot;pixel error&quot; instruction of the form:
<BR>
+ * 	perr	$ra,$rb,$rc
<BR>
+ */
<BR>
+emul_perr:
<BR>
+	cmpbge	$2,$3,$1
<BR>
+	zap	$3,$1,$4
<BR>
+	zapnot	$2,$1,$5
<BR>
+	zap	$2,$1,$2
<BR>
+	zapnot	$3,$1,$3
<BR>
+	or	$4,$5,$4 /* $4 = vector of max bytes */
<BR>
+	or	$2,$3,$3 /* $3 = vector of min bytes */
<BR>
+	subq	$4,$3,$3
<BR>
+	put_user_reg($0)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_perr
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_pklb
<BR>
+.ent	emul_pklb
<BR>
+/* emulating a &quot;pack longwords to bytes&quot; instruction of the form:
<BR>
+ * 	pklb	$rb,$rc
<BR>
+ */
<BR>
+emul_pklb:
<BR>
+	extbl	$3,0x4,$4
<BR>
+	extbl	$3,0x0,$5
<BR>
+	insbl	$4,0x1,$4
<BR>
+	and	$0,0x1f,$1
<BR>
+	or	$4,$5,$3
<BR>
+	put_user_reg($1)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_pklb
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_pkwb
<BR>
+.ent	emul_pkwb
<BR>
+/* emulating a &quot;pack words to bytes&quot; instruction of the form:
<BR>
+ * 	pkwb	$rb,$rc
<BR>
+ */
<BR>
+emul_pkwb:
<BR>
+	extbl	$3,0x6,$4
<BR>
+	extbl	$3,0x4,$5
<BR>
+	insbl	$4,0x3,$4
<BR>
+	insbl	$5,0x2,$5
<BR>
+	extbl	$3,0x2,$2
<BR>
+	or	$4,$5,$5
<BR>
+	extbl	$3,0x0,$3
<BR>
+	insbl	$2,0x1,$2
<BR>
+	or	$2,$3,$3
<BR>
+	and	$0,0x1f,$1
<BR>
+	or	$5,$3,$3
<BR>
+	put_user_reg($1)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_pkwb
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_unpkbl
<BR>
+.ent	emul_unpkbl
<BR>
+/* emulating a &quot;unpack bytes to longwords&quot; instruction of the form:
<BR>
+ * 	unpkbl	$rb,$rc
<BR>
+ */
<BR>
+emul_unpkbl:
<BR>
+	extbl	$3,0x1,$4
<BR>
+	extbl	$3,0x0,$5
<BR>
+	insbl	$4,0x4,$4
<BR>
+	and	$0,0x1f,$1
<BR>
+	or	$4,$5,$3
<BR>
+	put_user_reg($1)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_unpkbl
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_unpkbw
<BR>
+.ent	emul_unpkbw
<BR>
+/* emulating a &quot;unpack bytes to words&quot; instruction of the form:
<BR>
+ * 	unpkbw	$rb,$rc
<BR>
+ */
<BR>
+emul_unpkbw:
<BR>
+	extbl	$3,0x3,$4
<BR>
+	extbl	$3,0x2,$5
<BR>
+	insbl	$4,0x6,$4
<BR>
+	insbl	$5,0x4,$5
<BR>
+	extbl	$3,0x1,$2
<BR>
+	or	$4,$5,$5
<BR>
+	extbl	$3,0x0,$3
<BR>
+	insbl	$2,0x2,$2
<BR>
+	or	$2,$3,$3
<BR>
+	and	$0,0x1f,$1
<BR>
+	or	$5,$3,$3
<BR>
+	put_user_reg($1)
<BR>
+
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	call_pal PAL_rti
<BR>
+.end	emul_unpkbw
<BR>
+
<BR>
+.align 3
<BR>
+.globl	emul_mmx
<BR>
+.ent	emul_mmx
<BR>
+/* Emulates multimedia support instructions and sign extend instructions.
<BR>
+ * These instructions share a common form:
<BR>
+ * 	&lt;instr&gt;	$ra,$rb,$rc
<BR>
+ * or
<BR>
+ *	&lt;instr&gt; $ra,immediate8,$rc
<BR>
+ *
<BR>
+ * (the sign extend instructions do not use $ra)
<BR>
+ */
<BR>
+emul_mmx:
<BR>
+	stq	$3,24($30)
<BR>
+	stq	$4,32($30)
<BR>
+	stq	$5,40($30)
<BR>
+
<BR>
+	srl	$0,5,$4
<BR>
+	srl	$0,12,$2
<BR>
+	and	$4,0x3f,$4
<BR>
+	blbs	$2,21f
<BR>
+	/* the second parameter comes from a register */
<BR>
+	srl	$0,16,$1
<BR>
+	and	$1,0x1f,$1
<BR>
+	get_user_reg($1)
<BR>
+21:
<BR>
+	/* get the second parameter from an 8-bit immediate field (sign extd)*/
<BR>
+	srl	$0,13,$1
<BR>
+	and	$1,0xff,$1
<BR>
+	cmovlbs	$2,$1,$3 /* if needed, use imm field instead of $rb */
<BR>
+	/* now:	$0 = instruction
<BR>
+		$3 = $rb val
<BR>
+		$4 = operation function code */
<BR>
+	cmpeq	$4,0x1,$2
<BR>
+	beq	$4,emul_sexb
<BR>
+	bne	$2,emul_sexw
<BR>
+	cmpeq	$4,0x36,$1
<BR>
+	cmpeq	$4,0x37,$2
<BR>
+	bne	$1,emul_pkwb
<BR>
+	bne	$2,emul_pklb
<BR>
+	cmpeq	$4,0x34,$1
<BR>
+	cmpeq	$4,0x35,$2
<BR>
+	bne	$1,emul_unpkbw
<BR>
+	bne	$2,emul_unpkbl
<BR>
+	srl	$0,21,$1
<BR>
+	mov	$3,$2
<BR>
+	and	$1,0x1f,$1
<BR>
+	get_user_reg($1)
<BR>
+	cmpeq	$4,0x3a,$1
<BR>
+	cmpeq	$4,0x38,$5
<BR>
+	and	$0,0x1f,$0
<BR>
+	/* now:	$0 = $rc register index
<BR>
+		$2 = $rb val
<BR>
+		$3 = $ra val
<BR>
+		$4 = operation function code */
<BR>
+	bne	$1,emul_minub_8
<BR>
+	bne	$5,emul_minsb_8
<BR>
+	cmpeq	$4,0x3e,$1
<BR>
+	cmpeq	$4,0x3c,$5
<BR>
+	bne	$1,emul_maxsb_8
<BR>
+	bne	$5,emul_maxub_8
<BR>
+	cmpeq	$4,0x39,$1
<BR>
+	cmpeq	$4,0x3f,$5
<BR>
+	bne	$1,emul_minsw_4
<BR>
+	bne	$5,emul_maxsw_4
<BR>
+	cmpeq	$4,0x3b,$1
<BR>
+	cmpeq	$4,0x3d,$5
<BR>
+	bne	$1,emul_minuw_4
<BR>
+	bne	$5,emul_maxuw_4
<BR>
+	cmpeq	$4,0x31,$1
<BR>
+	bne	$1,emul_perr
<BR>
+	ldq	$5,40($30)
<BR>
+	ldq	$4,32($30)
<BR>
+	ldq	$3,24($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$0,0($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+	br	old_entIF
<BR>
+.end	emul_mmx
<BR>
+
<BR>
+#endif /* CONFIG_ALPHA_EXTINSNS */
<BR>
+
<BR>
&nbsp;.align 3
<BR>
&nbsp;.globl	entIF
<BR>
&nbsp;.ent	entIF
<BR>
&nbsp;entIF:
<BR>
+#ifdef CONFIG_ALPHA_EXTINSNS
<BR>
+	subq	$30,7*8,$30 /* 3 regs saved here, 3 or 4 more saved in fns */
<BR>
+	stq	$0,0($30)
<BR>
+	ldq	$0,8+7*8($30)
<BR>
+	stq	$1,8($30)
<BR>
+	ldl	$0,-4($0)
<BR>
+	stq	$2,16($30)
<BR>
+	srl	$0,26,$1
<BR>
+	and	$1,0x3f,$1
<BR>
+	cmpeq	$1,0x1c,$2
<BR>
+	bne	$2,emul_mmx
<BR>
+	cmpeq	$1,0x0a,$2
<BR>
+	bne	$2,emul_ldbu
<BR>
+	cmpeq	$1,0x0e,$2
<BR>
+	bne	$2,emul_stb
<BR>
+	cmpeq	$1,0x0c,$2
<BR>
+	cmpeq	$1,0x0d,$1
<BR>
+	bne	$2,emul_ldwu
<BR>
+	bne	$1,emul_stw
<BR>
+	ldq	$0,0($30)
<BR>
+	ldq	$1,8($30)
<BR>
+	ldq	$2,16($30)
<BR>
+	addq	$30,7*8,$30
<BR>
+old_entIF:
<BR>
+#endif /* CONFIG_ALPHA_EXTINSNS */
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;SAVE_ALL
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;lda	$8,0x3fff
<BR>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;lda	$26,ret_from_sys_call
<BR>

N‹§²æìr¸›zÇ§uéšŠ[hkeŠËkz«ž²ÚÞv­r‰°º{.nÇ+‰·š²Ø^²æãyËC¢z-±éÝŠÛhkeŠËkyØZµÊ&amp;<BR>
<P><!-- body="end" -->
<HR>
<P>
<UL>
<!-- next="start" -->
<LI><STRONG>Next message:</STRONG> <A HREF="0501.html">Brad Lucier: "Re: Serious problems on DS20"</A>
<LI><STRONG>Previous message:</STRONG> <A HREF="0499.html">Champigny, Michael: "Ethernet on Multia solved..."</A>
<LI><STRONG>In reply to:</STRONG> <A HREF="0405.html">Robert Harley: "Re: patch_v2: (on Alpha) emulating missing instructions"</A>
<!-- nextthread="start" -->
<LI><STRONG>Next in thread:</STRONG> <A HREF="0592.html">Richard Henderson: "Re: patch_v3: emulating missing instructions on Alpha"</A>
<LI><STRONG>Reply:</STRONG> <A HREF="0405.html">Luke Deller: "patch_v3: emulating missing instructions on Alpha"</A>
<!-- reply="end" -->
</UL>
<!-- trailer="footer" -->
<HR>
<P>
<SMALL>
<EM>
This archive was generated by <A HREF="http://www.www.fts.frontec.se/~dast/hypermail/">hypermail 2a22</A> 
: <EM>Thu Nov 04 1999 - 16:56:52 PST</EM>
</EM>
</SMALL>
</BODY>
</HTML>
