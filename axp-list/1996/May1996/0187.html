<!-- received="Sat May 11 12:37:19 1996 " -->
<!-- sent="Sat, 11 May 1996 12:37:19 -0700" -->
<!-- name="David Mosberger-Tang" -->
<!-- email="davidm@AZStarNet.com" -->
<!-- subject="Re: libc/libm-Tuning" -->
<!-- id="199605111937.MAA12086@koala.azstarnet.com" -->
<!-- inreplyto="libc/libm-Tuning" -->
<HTML><HEAD><META NAME="htdig-email" CONTENT="webmaster@redhat.com"><TITLE>Re: libc/libm-Tuning</TITLE>
</HEAD>
<BODY TEXT="#000000" BGCOLOR="#ececec"  LINK="#3333cc" VLINK="#666666"><h2>Re: libc/libm-Tuning</h2>

<b>David Mosberger-Tang</b> (<a href="mailto:davidm@AZStarNet.com"><i>davidm@AZStarNet.com</i></a>)<br>
<i>Sat, 11 May 1996 12:37:19 -0700</i>
<p>
<ul>
<li> <b>Messages sorted by:</b> <a href="date.html#187">[ date ]</a><a href="index.html#187">[ thread ]</a><a href="subject.html#187">[ subject ]</a><a href="author.html#187">[ author ]</a>
<!-- next="start" -->
<li> <b>Next message:</b> <a href="0188.html">Roman Gollent: "Re: linux on EB164+"</a>
<li> <b>Previous message:</b> <a href="0186.html">David Mosberger-Tang: "announcement: aboot-0.4 (plus updated SRM documentation)"</a>
<li> <b>Maybe in reply to:</b> <a href="0181.html">Markus Dickebohm: "libc/libm-Tuning"</a>
<!-- nextthread="start" -->
<li> <b>Next in thread:</b> <a href="0189.html">Robert Allen Barker: "Re: libc/libm-Tuning"</a>
<!-- reply="end" -->
</ul>
<!-- body="start" -->
<i>&gt;&gt;&gt;&gt;&gt; On Sat, 11 May 1996 13:39:41 +0200 (MET DST), Markus Dickebohm &lt;<a href="mailto:markus@dali.slip.uni-koeln.de">markus@dali.slip.uni-koeln.de</a>&gt; said:</i><br>
<p>
  Markus&gt; The used compilers: gcc2.7.1 on Linux gcc2.7.2 on Digital<br>
  Markus&gt; Unix (AFAIK /bin/as is used) Almost a factor of 6 :-((<br>
  Markus&gt; . This is much more than reported here before.<br>
<p>
That's an old one.  Try the sqrt() appended below.  It's one that's in<br>
glibc.  It will need some tweaking to get it to compile outside of<br>
glibc, but it shouldn't be too bad.  I'd be interested in hearing what<br>
performance looks like with this sqrt() implementation.<br>
<p>
	--david<br>
<pre>
--
/* Copyright (C) 1996 Free Software Foundation, Inc.
   Contributed by David Mosberger (<a href="mailto:davidm@cs.arizona.edu">davidm@cs.arizona.edu</a>).
   Based on public-domain C source that is Copyright 1995 Linus Torvalds.
<p>
This file is part of the GNU C Library.
<p>
The GNU C Library is free software; you can redistribute it and/or
modify it under the terms of the GNU Library General Public License as
published by the Free Software Foundation; either version 2 of the
License, or (at your option) any later version.
<p>
The GNU C Library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
Library General Public License for more details.
<p>
You should have received a copy of the GNU Library General Public
License along with the GNU C Library; see the file COPYING.LIB.  If
not, write to the Free Software Foundation, Inc., 675 Mass Ave,
Cambridge, MA 02139, USA.  */
<p>
/* This version is much faster than generic sqrt implementation, but
it doesn't handle exceptional values or the inexact flag.  Don't use
this if _IEEE_FP or _IEEE_FP_INEXACT is in effect. */
<p>
#ifndef _IEEE_FP
<p>
#include &lt;errnos.h&gt;
#include &lt;sysdep.h&gt;
#ifdef __linux__
# include &lt;alpha/regdef.h&gt;
#else
#include &lt;regdef.h&gt;
#endif
<p>
	.set noreorder
	.set noat
<p>
	.rdata
	.align 5	# align to cache line
<p>
	/* do all memory accesses relative to sqrtdata: */
sqrtdata:
<p>
#define DN			0x00
#define UP			0x08
#define HALF			0x10
#define ALMOST_THREE_HALF	0x18
#define T2			0x20
<p>
	.quad 0x3fefffffffffffff	/* DN = next(1.0) */
	.quad 0x3ff0000000000001	/* UP = prev(1.0) */
	.quad 0x3fe0000000000000	/* HALF = 0.5 */
	.quad 0x3ff7ffffffc00000	/* ALMOST_THREE_HALF = 1.5-2^-30 */
<p>
/* table T2: */
.long	 0x1500, 0x2ef8,   0x4d67,  0x6b02,  0x87be,  0xa395,  0xbe7a,  0xd866
.long	 0xf14a, 0x1091b, 0x11fcd, 0x13552, 0x14999, 0x15c98, 0x16e34, 0x17e5f
.long	0x18d03, 0x19a01, 0x1a545, 0x1ae8a, 0x1b5c4, 0x1bb01, 0x1bfde, 0x1c28d
.long	0x1c2de, 0x1c0db, 0x1ba73, 0x1b11c, 0x1a4b5, 0x1953d, 0x18266, 0x16be0
.long	0x1683e, 0x179d8, 0x18a4d, 0x19992, 0x1a789, 0x1b445, 0x1bf61, 0x1c989
.long	0x1d16d, 0x1d77b, 0x1dddf, 0x1e2ad, 0x1e5bf, 0x1e6e8, 0x1e654, 0x1e3cd
.long	0x1df2a, 0x1d635, 0x1cb16, 0x1be2c, 0x1ae4e, 0x19bde, 0x1868e, 0x16e2e
.long	0x1527f, 0x1334a, 0x11051,  0xe951,  0xbe01,  0x8e0d,  0x5924,  0x1edd
<p>
/*
 * Stack variables:
 */
#define K	0(sp)
#define Y	8(sp)
#define FSIZE	16
<p>
	.text
<p>
ENTRY(__sqrt)
	lda	sp, -FSIZE(sp)
	ldgp	gp, 4(pv)
<p>
	.frame sp,FSIZE,ra,0
	.prologue 0
<p>
	stt	$f16, K
	lda	t3, sqrtdata		/* load base address into t3 */
<p>
	fblt	$f16, negative
<p>
	# compute initial guess:
<p>
	ldah	t1, 0x5fe8
	ldq	t2, K
	ldt	$f12, HALF(t3)
	ldt	$f18, ALMOST_THREE_HALF(t3)
	srl	t2, 33, t0
	mult	$f16, $f12, $f11	/* $f11 = x * 0.5 */
	subl	t1, t0, t1
	addt	$f12, $f12, $f17	/* $f17 = 1.0 */
	srl	t1, 12, t0
	and	t0, 0xfc, t0
	addq	t0, t3, t0
	ldl	t0, T2(t0)
	addt	$f12, $f17, $f15	/* $f15 = 1.5 */
	subl	t1, t0, t1
	sll	t1, 32, t1
	stq	t1, Y
	ldt	$f14, DN(t3)
	ldt	$f13, Y
	addq	sp, FSIZE, sp
<p>
	mult	$f11, $f13, $f10	/* $f10 = (x * 0.5) * y */
	mult	$f10, $f13, $f10	/* $f10 = ((x * 0.5) * y) * y */
	subt	$f15, $f10, $f1		/* $f1 = (1.5 - 0.5*x*y*y) */
	mult	$f13, $f1, $f13		/* y' = $f13 = y*(1.5 - 0.5*x*y*y) */
	mult	$f11, $f13, $f11	/* $f11 = x * 0.5 * y' */
	mult	$f11, $f13, $f11	/* $f11 = (x * 0.5 * y') * y' */
	subt	$f18, $f11, $f1		/* $f1 = (1.5-2^-30) - 0.5*x*y'*y' */
	mult	$f13, $f1, $f13		/* y''=$f13 = y'*$f1 */
	subt	$f15, $f12, $f1		/* $f1 = (1.5 - 0.5) */
	ldt	$f15, UP(t3)
	mult	$f16, $f13, $f10	/* z = $f10 = x * y'' */
	mult	$f10, $f13, $f11	/* $f11 = z*y'' */
	mult	$f10, $f12, $f12	/* $f12 = z*0.5 */
	subt	$f1, $f11, $f1		/* $f1 = 1 - z*y'' */
	mult	$f12, $f1, $f12		/* $f12 = z*0.5*(1 - z*y'') */
	addt	$f10, $f12, $f0		/* z'=res=$f0= z + z*0.5*(1 - z*y'') */
<p>
	mult/c	$f0, $f14, $f12		/* zp = z' * DN */
	mult/c	$f0, $f15, $f11		/* zn = z' * UP */
	mult/c	$f0, $f12, $f1		/* $f1 = z' * zp */
	mult/c	$f0, $f11, $f15		/* $f15 = z' * zn */
<p>
	subt	$f1, $f16, $f13		/* y1 = z'*zp - x */
	subt	$f15, $f16, $f15	/* y2 = z'*zn - x */
<p>
	fcmovge	$f13, $f12, $f0		/* res = (y1 &gt;= 0) ? zp : res */
	fcmovlt	$f15, $f11, $f0		/* res = (y1 &lt;  0) ? zn : res */
<p>
	ret
<p>
negative:
	lda	t0, errno		# errno = DEOM
	lda	t1, -1
	stq	t1, K
	ldi	t1, EDOM
	stl	t1, 0(t0)
	ldt	$f0, K			# res = (double) 0xffffffff
	addq	sp, FSIZE, sp
	ret
<p>
	.end __sqrt
<p>
weak_alias(__sqrt, sqrt)
<p>
#endif /* !_IEEE_FP */
</pre>
<!-- body="end" -->
<p>
<ul>
<!-- next="start" -->
<li> <b>Next message:</b> <a href="0188.html">Roman Gollent: "Re: linux on EB164+"</a>
<li> <b>Previous message:</b> <a href="0186.html">David Mosberger-Tang: "announcement: aboot-0.4 (plus updated SRM documentation)"</a>
<li> <b>Maybe in reply to:</b> <a href="0181.html">Markus Dickebohm: "libc/libm-Tuning"</a>
<!-- nextthread="start" -->
<li> <b>Next in thread:</b> <a href="0189.html">Robert Allen Barker: "Re: libc/libm-Tuning"</a>
<!-- reply="end" -->
</ul>
<br clear=all>
<hr>
<center>
<a href="mailto:webmaster@redhat.com"   target="">Feedback</a> | 
<a href="http://www.redhat.com/products/"   target="">Store</a> | 
<a href="http://www.redhat.com/news/"   target="">News</a> | 
<a href="http://www.redhat.com/support/"   target="">Support</a> | 
<a href="http://www.redhat.com/support/docs/errata.html"   target="">Product Errata</a> | 
<a href="http://www.redhat.com/redhat/"   target="">About Us</a> | 
<a href="http://www.redhat.com/linux-info/"   target="">Linux Info</a> | 
<a href="http://www.redhat.com/search/"   target="">Search</a> | 
<a href="http://www.redhat.com/jumplist.phtml"   target="">JumpWords</a>
<br>
<a href="http://www.redhat.com/cgi-bin/frames.phtml?fr=n"  _top target="_top">No Frames</a> | 
<a href="http://www.redhat.com/cgi-bin/frames.phtml?fr=y"  _top target="_top">Show Frames</a>
</center>
<p align=center>
Copyright &copy; 1995-1997 Red Hat Software. <a href="http://www.redhat.com/redhat/website.html#legal"   target="">Legal notices</a>
</p>
</BODY></HTML>
